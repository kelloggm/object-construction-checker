plugins {
    id 'java-library'

}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

sourceCompatibility = 1.8

configurations {
    // for putting Error Prone javac in bootclasspath for running tests
    errorproneJavac
}

dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    implementation "org.checkerframework:checker:${versions.checkerFramework}"
    compileOnly "com.google.errorprone:javac:9+181-r4173-1"

    // Export qualifiers to upstream dependencies, for convenience
    api project(':returnsrcvr-qual')

    // Testing
    testImplementation "junit:junit:4.12"
    testCompile "org.checkerframework:framework-test:${versions.checkerFramework}"

    // AutoValue support
    implementation "com.google.auto.value:auto-value-annotations:${versions.autoValue}"
    testCompile "com.google.auto.value:auto-value-annotations:${versions.autoValue}"
    testCompile "com.google.auto.value:auto-value:${versions.autoValue}"

    // Lombok support
    testCompile "org.projectlombok:lombok:${versions.lombok}"

    errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"
}

tasks.withType(JavaCompile).all {
    options.compilerArgs.add("-Xlint:all")
}

// run google java format
spotless {
    // uncomment this line to temporarily disable spotless (i.e. when debugging)
    // enforceCheck = false
    java {
        googleJavaFormat()
    }
}

test {
    inputs.files("tests/all-systems", "tests/autovalue", "tests/lombok", "tests/returnsrcvr")
    if (!JavaVersion.current().java9Compatible) {
        jvmArgs "-Xbootclasspath/p:${configurations.errorproneJavac.asPath}"
    }
}

apply from: rootProject.file("gradle-mvn-push.gradle")
